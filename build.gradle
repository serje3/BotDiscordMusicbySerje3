plugins {
    id 'application'
    id 'org.jetbrains.kotlin.jvm'
    id "io.sentry.jvm.gradle" version "4.4.0"
}

group 'org.serje3'
version '1.0-SNAPSHOT'

ext {
    javaMainClass = "org.serje3.BotApplication"
}

application {
    mainClassName = javaMainClass
}

repositories {
    mavenCentral()
    maven {
        url 'https://maven.arbjerg.dev/releases'
    }
    maven {
        url 'https://maven.arbjerg.dev/snapshots'
    }
    maven {
        name "topiWTFReleases"
        url "https://maven.topi.wtf/releases"
    }
    maven {
        url 'https://m2.dv8tion.net/releases'
    }
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation "dev.arbjerg:lavalink-client:2.2.0"
    implementation "dev.arbjerg:lavaplayer:2.0.2"
    implementation "net.dv8tion:JDA:5.0.0-beta.15"
    implementation "com.github.topi314.lavasearch:lavasearch:1.0.0-beta.2"
    implementation "com.github.topi314.lavasrc:lavasrc:4.0.0-beta.7"

    implementation 'org.slf4j:slf4j-api:2.0.9'
    implementation 'org.slf4j:slf4j-simple:2.0.9'

    implementation 'com.google.code.gson:gson:2.10.1'

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

}

tasks.register('runWithJavaExec', JavaExec) {
    group = "Execution"
    description = "Run the main class with JavaExecTask"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = javaMainClass
}

tasks.register('runWithExecJarOnClassPath', Exec) {
    dependsOn jar
    group = "Execution"
    description = "Run the mainClass from the output jar in classpath with ExecTask"
    commandLine "java", "-classpath", jar.archiveFile.get(), javaMainClass
}


jar {
    manifest {
        attributes(
                "Main-Class": javaMainClass
        )
    }
}

tasks.register('runWithExecJarExecutable', Exec) {
    dependsOn jar
    group = "Execution"
    description = "Run the output executable jar with ExecTask"
    commandLine "java", "-jar", jar.archiveFile.get()
}

test {
    useJUnitPlatform()
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
compileKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}

sentry {
    // Generates a JVM (Java, Kotlin, etc.) source bundle and uploads your source code to Sentry.
    // This enables source context, allowing you to see your source
    // code as part of your stack traces in Sentry.
    includeSourceContext = true

    org = "sergey-erin"
    projectName = "java"
    authToken = System.getenv("SENTRY_AUTH_TOKEN")
}